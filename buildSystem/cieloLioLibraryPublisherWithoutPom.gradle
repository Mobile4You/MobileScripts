apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

// Maven
publishing {

    publications {

        aar(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            if (!project.hasProperty('release')) {
                version = "${project.version}-SNAPSHOT"
            }

            // Publishing compiled code in aar or jar
            if (project.plugins.hasPlugin("com.android.library")) {
                artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"
            } else {
                artifact "${project.buildDir}/libs/${project.name}-${project.version}.jar"
            }
        }
    }
}

// Jfrog
artifactory {
    contextUrl = artifactoryContextUrl
    publish {
        repository {

            def artifactoryRepo = ''

            if (project.hasProperty('release')) {
                if (project.hasProperty('artifactoryReleaseLocal')) {
                    artifactoryRepo = artifactoryReleaseLocal
                }
            } else if (project.hasProperty('artifactorySnapshotLocal')) {
                artifactoryRepo = artifactorySnapshotLocal
            }

            repoKey = artifactoryRepo
            username = artifactoryUser
            password = artifactoryPassword
            maven = true
        }
        defaults {
            publishArtifacts = true
            publications('aar')
            publishPom = true
            publishIvy = true
        }
    }
    resolve {
        repository {
            repoKey = artifactoryResolveRepoKey
            username = artifactoryUser
            password = artifactoryPassword
        }
    }
}
